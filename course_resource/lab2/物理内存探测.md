操作系统需要知道了解整个计算机系统中的物理内存如何分布的，哪些可用，哪些不可用。其基本方法是通过BIOS中断调用来帮助完成的。其中BIOS中断调用必须在实模式下进行，
--->所以在bootloader进入保护模式前完成这部分工作相对比较合适。
这些部分由boot/bootasm.S中从probe_memory处到finish_probe处的代码部分完成。通过BIOS中断获取内存可调用参数为e820h的INT
15h BIOS中断。
---------
IOS通过系统内存映射地址描述符（Address Range
Descriptor）格式来表示系统物理内存布局，其具体表示如下：
    Offset  Size    Description
    00h    8字节   base address               #系统内存块基地址
    08h    8字节   length in bytes            #系统内存大小
    10h    4字节   type of address range     #内存类型
---------
probe_memory:
    movl $0, 0x8000
    xorl %ebx, %ebx
    movw $0x8004, %di
start_probe:
    movl $0xE820, %eax
    movl $20, %ecx
    movl $SMAP, %edx
    int $0x15
    jnc cont
    movw $12345, 0x8000
    jmp finish_probe
cont:
    addw $20, %di
    incl 0x8000
    cmpl $0, %ebx
    jnz start_probe
finish_probe:
--------
看下面的(Values for System Memory Map address type)

    Values for System Memory Map address type:
    01h    memory, available to OS
    02h    reserved, not available (e.g. system ROM, memory-mapped device)
    03h    ACPI Reclaim Memory (usable by OS after reading ACPI tables)
    04h    ACPI NVS Memory (OS is required to save this memory between NVS sessions)
    other  not defined yet -- treat as Reserved
-----------
INT15h BIOS中断的详细调用参数:

    eax：e820h：INT 15的中断调用参数；
    edx：534D4150h (即4个ASCII字符“SMAP”) ，这只是一个签名而已；
    ebx：如果是第一次调用或内存区域扫描完毕，则为0。 如果不是，则存放上次调用之后的计数值；
    ecx：保存地址范围描述符的内存大小,应该大于等于20字节；
        大小等于struct e820map的成员变量map的大小
    es:di：指向保存地址范围描述符结构的缓冲区，BIOS把信息写入这个结构的起始地址。
------------
此中断的返回值为:

    eflags的CF位：若INT 15中断执行成功，则不置位，否则置位；
        如果eflags的CF位为0，则表示还有内存段需要探测
    eax：534D4150h ('SMAP') ；
    es:di：指向保存地址范围描述符的缓冲区,此时缓冲区内的数据已由BIOS填写完毕
    ebx：下一个地址范围描述符的计数地址
    ecx    ：返回BIOS往ES:DI处写的地址范围描述符的字节大小
    ah：失败时保存出错代码
--------------
这样，我们通过调用INT 15h
BIOS中断，递增di的值（20的倍数），让BIOS帮我们查找出一个一个的内存布局entry，并放入到一个保存地址范围描述符结构的缓冲区中，供后续的ucore进一步进行物理内存管理。这个缓冲区结构定义在memlayout.h中：struct e820map
#define E820MAX             20      // number of entries in E820MAP
#define E820_ARM            1       // address range memory
#define E820_ARR            2       // address range reserved
    struct e820map {
                      int nr_map;//位于0x8000
                      struct {
                                        long long addr;
                                        long long size;
                                        long type;
                      } map[E820MAX];   //每一个数组就是一个入口
    };

    这里的nr_map是内存段的数量
    每个内存段由struct e820entry表示
    addr字段表示内存段的起始地址
    size字段表示内存段的大小
    type表示内存段的类型，比如E820_RAM表示可用内存
    E820MAX是一个宏，为20，说明最多可以有20个内存段

-------------------------------------------------
    probe_memory:
    //对0x8000处的32位单元清零,即给位于0x8000处的
    //struct e820map的成员变量nr_map清零
                      movl $0, 0x8000
                      xorl %ebx, %ebx
    //表示设置调用INT 15h BIOS中断后，BIOS返回的映射地址描述符的起始地址
                      movw $0x8004, %di
    start_probe:
                      movl $0xE820, %eax // INT 15的中断调用参数
    //设置地址范围描述符的大小为20字节，其大小等于struct e820map的成员变量map的大小
                      movl $20, %ecx
    //设置edx为534D4150h (即4个ASCII字符“SMAP”)，这是一个约定
                      movl $SMAP, %edx
    //调用int 0x15中断，要求BIOS返回一个用地址范围描述符表示的内存段信息
                      int $0x15
    //如果eflags的CF位为0，则表示还有内存段需要探测
                      jnc cont
    //探测有问题，结束探测
                      movw $12345, 0x8000
                      jmp finish_probe
    cont:
    //设置下一个BIOS返回的映射地址描述符的起始地址
                      addw $20, %di
    //递增struct e820map的成员变量nr_map
                      incl 0x8000
    //如果INT0x15返回的ebx为零，表示探测结束，否则继续探测
                      cmpl $0, %ebx
                      jnz start_probe
    finish_probe:

--------------------
从上面代码可以看出，要实现物理内存空间的探测，大体上只需要3步：
1.    设置一个存放内存映射地址描述符的物理地址(这里是0x8000)
2.    将e820作为参数传递给INT 15h中断
3.    通过检测eflags的CF位来判断探测是否结束。如果没有结束， 设置存放下一个内存映射地址描述符的物理地址，然后跳到步骤2；如果结束，则程序结束

--------------------
上述代码正常执行完毕后，在0x8000地址处保存了从BIOS中获得的内存分布信息，此信息按照struct e820map的设置来进行填充。
--->可以理解为是内存映射地址描述符表)。 当完成物理内存空间的探测后， 我们就可以通过这个表来了解物理内存空间的分布情况了。
这部分信息将在bootloader启动ucore后，由ucore的page_init函数来根据struct
e820map的memmap（定义了起始地址为0x8000）来完成对整个机器中的物理内存的总体管理。